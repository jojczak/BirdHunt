import org.jetbrains.kotlin.gradle.dsl.JvmTarget

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
  namespace "pl.jojczak.birdhunt"
  compileSdk 35
  sourceSets {
    main {
      manifest.srcFile 'AndroidManifest.xml'
      java.setSrcDirs(['src/main/java', 'src/main/kotlin'])
      aidl.setSrcDirs(['src/main/java', 'src/main/kotlin'])
      renderscript.setSrcDirs(['src/main/java', 'src/main/kotlin'])
      res.setSrcDirs(['res'])
      assets.setSrcDirs(['../assets'])
      jniLibs.setSrcDirs(['libs'])
    }
  }
  packagingOptions {
		resources {
			excludes += ['META-INF/robovm/ios/robovm.xml', 'META-INF/DEPENDENCIES.txt', 'META-INF/DEPENDENCIES',
                   'META-INF/dependencies.txt', '**/*.gwt.xml']
			pickFirsts += ['META-INF/LICENSE.txt', 'META-INF/LICENSE', 'META-INF/license.txt', 'META-INF/LGPL2.1',
                     'META-INF/NOTICE.txt', 'META-INF/NOTICE', 'META-INF/notice.txt']
		}
  }
  flavorDimensions = ["PGS"]
  productFlavors {
    PGS_On {
      getIsDefault().set(true)
      dimension "PGS"
    }
    PGS_Off {
      dimension "PGS"
    }
  }
  defaultConfig {
    applicationId 'pl.jojczak.birdhunt'
    minSdkVersion 31
    targetSdkVersion 35
    versionCode 10
    versionName project.findProperty('projectVersion')
    multiDexEnabled true
  }
  compileOptions {
    sourceCompatibility "11"
    targetCompatibility "11"
    coreLibraryDesugaringEnabled true
  }
  signingConfigs {
    release {
      storeFile file("keystore.jks")
      storePassword System.getenv("KEYSTORE_PASSWORD")
      keyPassword System.getenv("KEY_PASSWORD")
      keyAlias System.getenv("KEY_ALIAS")
      enableV1Signing true
      enableV2Signing true
      enableV3Signing true
      enableV4Signing true
    }
  }
  buildTypes {
    release {
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      signingConfig signingConfigs.release
    }
  }

  kotlin.compilerOptions.jvmTarget.set(JvmTarget.JVM_11)

}

repositories {
  // needed for AAPT2, may be needed for other tools
  google()
}

configurations { natives }

dependencies {
  coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.5'
  implementation "com.badlogicgames.gdx:gdx-backend-android:$gdxVersion"
  implementation fileTree(dir: 'libs', include: ['*.jar'])
  implementation project(':core')

  natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-arm64-v8a"
  natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi-v7a"
  natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86"
  natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86_64"

  implementation "androidx.core:core-ktx:1.15.0"
  implementation "androidx.core:core-splashscreen:1.2.0-beta01"
  implementation "com.google.android.play:review:2.0.2"
  implementation "com.google.android.play:review-ktx:2.0.2"
  PGS_OnImplementation "com.google.android.gms:play-services-games-v2:20.1.2"
}

// Called every time gradle gets executed, takes the native dependencies of
// the natives configuration, and extracts them to the proper libs/ folders
// so they get packed with the APK.
tasks.register('copyAndroidNatives') {
  doFirst {
    file("libs/armeabi-v7a/").mkdirs()
    file("libs/arm64-v8a/").mkdirs()
    file("libs/x86_64/").mkdirs()
    file("libs/x86/").mkdirs()

    configurations.natives.copy().files.each { jar ->
      def outputDir = null
      if(jar.name.endsWith("natives-armeabi-v7a.jar")) outputDir = file("libs/armeabi-v7a")
      if(jar.name.endsWith("natives-arm64-v8a.jar")) outputDir = file("libs/arm64-v8a")
      if(jar.name.endsWith("natives-x86_64.jar")) outputDir = file("libs/x86_64")
      if(jar.name.endsWith("natives-x86.jar")) outputDir = file("libs/x86")
      if(outputDir != null) {
        copy {
          from zipTree(jar)
          into outputDir
          include "*.so"
        }
      }
    }
  }
}

tasks.matching { it.name.contains("merge") && it.name.contains("JniLibFolders") }.configureEach { packageTask ->
  packageTask.dependsOn 'copyAndroidNatives'
}

tasks.register('run', Exec) {
  def path
  def localProperties = project.file("../local.properties")
  if (localProperties.exists()) {
    Properties properties = new Properties()
    localProperties.withInputStream { instr ->
      properties.load(instr)
    }
    def sdkDir = properties.getProperty('sdk.dir')
    if (sdkDir) {
      path = sdkDir
    } else {
      path = "$System.env.ANDROID_SDK_ROOT"
    }
  } else {
    path = "$System.env.ANDROID_SDK_ROOT"
  }

  def adb = path + "/platform-tools/adb"
  commandLine "$adb", 'shell', 'am', 'start', '-n', 'pl.jojczak.birdhunt/pl.jojczak.birdhunt.android.AndroidLauncher'
}

eclipse.project.name = appName + "-android"
